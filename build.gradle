plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jreleaser' version '1.14.0'
}

group = 'com.lmax'
version = '1.0.0'

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = '10.4'
    configFile = file('config/checkstyle/checkstyle.xml')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    clientMain {
        java {
            srcDir file('client/src/main/java')
        }
    }
    clientTest {
        compileClasspath += sourceSets.clientMain.output
        runtimeClasspath += sourceSets.clientMain.output
        java {
            srcDir file('client/src/test/java')
        }
    }
    messageEncodingMain {
        java {
            srcDir file('message-encoding/src/main/java')
        }
    }
    messageEncodingTestSupport {
        java {
            compileClasspath += sourceSets.messageEncodingMain.output + sourceSets.clientMain.output
            srcDir file('message-encoding/src/test-support/java')
        }
    }
    messageEncodingTest {
        java {
            compileClasspath += sourceSets.messageEncodingMain.output + sourceSets.messageEncodingTestSupport.output
            runtimeClasspath += sourceSets.messageEncodingMain.output + sourceSets.messageEncodingTestSupport.output
            srcDir file('message-encoding/src/test/java')
        }
        resources.srcDir file('src/test/resources')
    }
    messageEncodingIntegrationTest {
        java {
            compileClasspath += sourceSets.messageEncodingTestSupport.output + sourceSets.clientMain.output
            runtimeClasspath += sourceSets.messageEncodingMain.output + sourceSets.messageEncodingTestSupport.output + sourceSets.clientMain.output
            srcDir file('message-encoding/src/integration-test/java')
        }
        resources.srcDir file('message-encoding/src/integration-test/resources')
    }
    messageEncodingJmhTest {
        java {
            compileClasspath += sourceSets.messageEncodingMain.output + sourceSets.messageEncodingTestSupport.output
            runtimeClasspath += sourceSets.messageEncodingMain.output + sourceSets.messageEncodingTestSupport.output
            srcDir file('message-encoding/src/jmh-test/java')
        }
    }
}

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:10.4'

    clientMainImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    clientTestImplementation 'org.assertj:assertj-core:3.26.3'
    clientTestImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    clientTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'

    // test support dependencies
    messageEncodingTestSupportImplementation 'com.lmax:simple-dsl:3.0.0'
    messageEncodingTestSupportImplementation 'org.assertj:assertj-core:3.26.3'
    messageEncodingTestSupportImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    messageEncodingTestSupportImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    messageEncodingTestSupportImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    messageEncodingTestSupportImplementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'

    // test dependencies
    messageEncodingTestImplementation 'org.assertj:assertj-core:3.26.3'
    messageEncodingTestImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    messageEncodingTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    messageEncodingTestImplementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'
    messageEncodingTestImplementation 'org.slf4j:slf4j-api:2.0.13'
    messageEncodingTestImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    messageEncodingTestImplementation 'net.i2p.crypto:eddsa:0.3.0'
    messageEncodingTestImplementation 'org.bitcoinj:bitcoinj-core:0.16.3'

    // integration test dependencies
    messageEncodingIntegrationTestImplementation 'org.testcontainers:testcontainers:1.19.8'
    messageEncodingIntegrationTestImplementation 'com.lmax:simple-dsl:3.0.0'
    messageEncodingIntegrationTestImplementation 'org.assertj:assertj-core:3.26.3'
    messageEncodingIntegrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    messageEncodingIntegrationTestImplementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
    messageEncodingIntegrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    messageEncodingIntegrationTestImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
    messageEncodingIntegrationTestImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    messageEncodingIntegrationTestImplementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'
    messageEncodingIntegrationTestImplementation 'org.slf4j:slf4j-api:2.0.13'

    // jmh test dependencies
    messageEncodingJmhTestImplementation 'org.openjdk.jmh:jmh-core:1.37'
    messageEncodingJmhTestImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    messageEncodingJmhTestImplementation 'net.i2p.crypto:eddsa:0.3.0'
    messageEncodingJmhTestImplementation 'org.bitcoinj:bitcoinj-core:0.16.3'
    messageEncodingJmhTestAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
}

task setUpGitHooks(type: Exec, description: 'Add a pre-commit git hook that runs gradle check & test tasks') {
    def hooksFolder = file('.githooks').getAbsolutePath()
    commandLine 'git', 'config', 'core.hooksPath', hooksFolder
}

task messageEncodingTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs the solana message encoding unit tests .'
    group = 'verification'
    testClassesDirs = sourceSets.messageEncodingTest.output.classesDirs
    classpath = sourceSets.messageEncodingTest.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

task clientTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs the solana client unit tests .'
    group = 'verification'
    testClassesDirs = sourceSets.clientTest.output.classesDirs
    classpath = sourceSets.clientTest.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

task messageEncodingIntegrationTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs the solana message encoding integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.messageEncodingIntegrationTest.output.classesDirs
    classpath = sourceSets.messageEncodingIntegrationTest.runtimeClasspath + sourceSets.messageEncodingTestSupport.runtimeClasspath
    shouldRunAfter test
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
    maxHeapSize = "1g"
}

check.dependsOn clientTest
check.dependsOn messageEncodingTest
check.dependsOn messageEncodingIntegrationTest

tasks.processTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// FIXME: including in gradle.yaml and release.yaml
javadoc {
    title = 'solana4j'

    source = fileTree('solana-message-encoding/src/main/java').include('**/*.java')

    options.addStringOption('XDignore.symbol.file', '-quiet')
    options.author = true
    options.bottom = "<i>Copyright &#169; 2011 - ${Calendar.instance[Calendar.YEAR]} LMAX Ltd. All Rights Reserved.</i>"
    options.use = true
    options.version = true
    options.showFromPublic()
    options.overview = file('overview.html').absolutePath
}

jreleaser {
    files {
        active = 'ALWAYS'
        glob {
            pattern = 'build/staging-deploy/**/*.jar'
            pattern = 'build/staging-deploy/**/*.pom'
            pattern = 'build/staging-deploy/**/*.module'
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'MEMORY'
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    applyMavenCentralRules = true
                }
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.lmax'
            artifactId = 'solana4j'

            from components.java

            pom {
                name = 'solana4j'
                description = 'A Java library for natively interacting with the Solana blockchain.'
                url = 'https://github.com/LMAX-Exchange/solana4j'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'jamesm'
                        name = 'Matthew James'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/LMAX-Exchange/solana4j.git'
                    developerConnection = 'scm:git:ssh://github.com/LMAX-Exchange/solana4j.git'
                    url = 'https://github.com/LMAX-Exchange/solana4j'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

