plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jreleaser' version '1.14.0'
}

group = 'com.lmax'
version = '1.0.0'

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = '10.4'
    configFile = file('config/checkstyle/checkstyle.xml')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly

    testSupportImplementation.extendsFrom testImplementation
    testSupportRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    testSupport {
        java {
            compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
            runtimeClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
            srcDir file('src/test-support/java')
        }
    }
    test {
        java {
            compileClasspath += sourceSets.main.output + sourceSets.testSupport.output
            runtimeClasspath += sourceSets.main.output + sourceSets.testSupport.output
            srcDir file('src/test/java')
        }
        resources.srcDir file('src/test/resources')
    }
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath + sourceSets.testSupport.output
            runtimeClasspath += sourceSets.main.output + configurations.testRuntimeClasspath + sourceSets.testSupport.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
    jmh {
        java {
            compileClasspath += sourceSets.main.output + sourceSets.testSupport.output
            runtimeClasspath += sourceSets.main.output + sourceSets.testSupport.output
            srcDir file('src/jmh/java')
        }
    }
}

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:10.4'

    // test support dependencies
    testSupportImplementation 'org.testcontainers:testcontainers:1.19.8'
    testSupportImplementation 'com.lmax:simple-dsl:3.0.0'
    testSupportImplementation 'org.assertj:assertj-core:3.25.3'
    testSupportImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testSupportImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testSupportImplementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
    testSupportImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    testSupportImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
    testSupportImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'

    // test dependencies
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'
    testImplementation 'org.slf4j:slf4j-api:2.0.13'
    testImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'

    // integration test dependencies
    integrationTestImplementation 'org.testcontainers:testcontainers:1.19.8'
    integrationTestImplementation 'com.lmax:simple-dsl:3.0.0'
    integrationTestImplementation 'org.assertj:assertj-core:3.25.3'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    integrationTestImplementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
    integrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    integrationTestImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
    integrationTestImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    integrationTestImplementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'
    integrationTestImplementation 'org.slf4j:slf4j-api:2.0.13'

    // jmh test dependencies
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.37'
    jmhImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    jmhImplementation 'net.i2p.crypto:eddsa:0.3.0'
    jmhImplementation 'org.bitcoinj:bitcoinj-core:0.16.3'

    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = 'runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath + sourceSets.testSupport.runtimeClasspath
    shouldRunAfter test
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

tasks.processTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

javadoc {
    title = 'solana4j'

    source = fileTree('src/main/java').include('**/*.java')

    options.addStringOption('XDignore.symbol.file', '-quiet')
    options.author = true
    options.bottom = "<i>Copyright &#169; 2011 - ${Calendar.instance[Calendar.YEAR]} LMAX Ltd. All Rights Reserved.</i>"
    options.use = true
    options.version = true
    options.showFromPublic()
    options.overview = file('overview.html').absolutePath
}


task setUpGitHooks(type: Exec, description: 'Add a pre-commit git hook that runs gradle check & test tasks') {
    def hooksFolder = file('.githooks').getAbsolutePath()
    commandLine 'git', 'config', 'core.hooksPath', hooksFolder
}

check.dependsOn integrationTest

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.lmax'
            artifactId = 'solana4j'

            from components.java

            pom {
                name = 'solana4j'
                description = 'A Java library for natively interacting with the Solana blockchain.'
                url = 'https://github.com/LMAX-Exchange/solana4j'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'jamesm'
                        name = 'Matthew James'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/LMAX-Exchange/solana4j.git'
                    developerConnection = 'scm:git:ssh://github.com/LMAX-Exchange/solana4j.git'
                    url = 'https://github.com/LMAX-Exchange/solana4j'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('target/staging-deploy')
                }
            }
        }
    }
}