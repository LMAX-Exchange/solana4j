plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.autonomousapps.dependency-analysis' version '2.2.0'
}

def solanaVersion = project.property('SOLANA_VERSION')

group = 'com.lmax'
version = '1.1.1'

dependencyAnalysis {
    issues {
        all {
            onAny {
                severity('fail')
            }
        }
    }
}

allprojects {
    ext {
        solanaVersion = project.property('SOLANA_VERSION')
    }
}

subprojects {
    checkstyle {
        toolVersion = '10.4'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    check.dependsOn buildHealth

    group = rootProject.group
    version = rootProject.version
}

task setUpGitHooks(type: Exec, description: 'Add a pre-commit git hook that runs gradle check & test tasks') {
    def hooksFolder = file('.githooks').getAbsolutePath()
    commandLine 'git', 'config', 'core.hooksPath', hooksFolder
}
